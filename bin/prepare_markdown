#!/bin/bash
awk 'BEGIN {
    in_code = 0;
	ref_idx = 1;
}
substr($1, 1, 3) == "```" {
    language = substr($1, 4)
    language_class = length(language) ? "language-" language : "";
    if (in_code == 0) {
        print "<pre class=\"line-numbers " language_class "\" data-language=\"" language "\">";
        print "<a class=\"btn btn-sm btn-purple clipboard-copy\" data-original-title=\"Copied!\">Copy</a>";
        print "<span class=\"language-name\">" language "</span>";
        print "<code class=\"" language_class "\">";
    } else {
        print "</code></pre>";
    }
    in_code = (in_code + 1) % 2;
}
(substr($1, 1, 1) != "#" || in_code != 0) && substr($1, 1, 3) != "```"{
	print $0
}
substr($1, 1, 1) == "#" && in_code == 0{
	match($0, "[^# ]", match_ret);
    curr_level = length($1);
	print "<h" curr_level " id=\"ref-" (ref_idx ++) "\">" substr($0, match_ret[0, "start"]) "</h" curr_level ">";
}
' "$1" > "$2"
